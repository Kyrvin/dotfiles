# Maintainer: Patrick Keating <kyrvin3 at gmail dot com>

_pkgname=st
pkgname=${_pkgname}-kyrvin
pkgver=0.7
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('x86_64')
license=('MIT')
depends=('libxft')
optdepends=('ttf-liberation: default font')
makedepends=('ncurses' 'libxext')
url="http://st.suckless.org"
source=("http://dl.suckless.org/st/$_pkgname-$pkgver.tar.gz"
        'st-256color.desktop'
        'st-gruvbox-0.7.diff'
        'st-scrollback-0.7.diff'
        'st-hidecursor-20160727-308bfbf.diff'
       )
sha1sums=('358bca5bfc6ee85352dcd4f45f13f78474e5de42'
          'aaaab2e2b879fcfd95ceaac522831ef844af2c8c'
          '4fad7891bd7903511693885e96c82a6ed491c1a7'
          'f42d0b0ae25491792acbc6b3ffab025027957a13'
          'd2e764c601549f44909edbc2d982d303781f95d8'
         )

provides=("$_pkgname")
conflicts=("$_pkgname")

prepare() {
  cd $srcdir/$_pkgname-$pkgver

  patch <$srcdir/st-gruvbox-0.7.diff
  patch <$srcdir/st-scrollback-0.7.diff
  patch <$srcdir/st-hidecursor-20160727-308bfbf.diff

  sed -i '/\@tic /d' Makefile
}

build() {
  cd $srcdir/$_pkgname-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$_pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$_pkgname/README"
  install -Dm644 "$srcdir/st-256color.desktop" "$pkgdir/usr/share/applications/st-256color.desktop"
}
